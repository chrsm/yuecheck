import 'lfs'

exists = (path) -> (lfs.attributes path, 'mode') != nil
is_dir = (dir) -> (lfs.attributes dir, 'mode') == 'directory'

read_file = (path) ->
  fp, err = io.open path, 'r'
  if err
    error err
  src = fp\read 'a*'
  fp\close!

  src

write_file = (path, data) ->
  fp, err = io.open path, 'w'
  if err
    error err
  fp\write data
  fp\close!

find_files = (path, ext, exclude = {}) ->
  unless is_dir path
    return nil, error "(#{ path }, #{ ext }) is not a directory"

  if (path\sub #path, #path) == '/'
    path = path\sub 1, #path - 1

  files = {}
  for ent in lfs.dir path
    continue if ent in { '.', '..' }

    p = "#{ path }/#{ ent }"

    skip = for v in *exclude
      if p\match v
        break true

    if skip
      continue

    if is_dir p
      -- skip common dirs; may need to add more
      if p\match '%.git$'
        continue

      files = { ...files, ...(find_files p, ext, exclude) }
    elseif p\match "%.#{ ext }$"
      files[] = p

  files

export default {
  :exists
  :is_dir

  :read_file
  :write_file

  :find_files
}
