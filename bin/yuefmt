#!/usr/bin/env -S yue -e
--[[
  yuefmt

  yuefmt is an opinionated linter for Yuescript
]]
import 'yue'
import 'argparse'
import 'dkjson' as json

import 'yuecheck.formatter'
import 'yuecheck.internal.fs'

argp = argparse!
  name: 'yuefmt'
  description: 'tool for formatting yue code'
  epilog: 'see https://github.com/chrsm/yuecheck'

with argp
  dof = \argument 'dir_or_file'
  dof\args '?'

  d = \option '-d --directory', 'directory containing files to format (requires -w)'
  d\args 1

  f = \option '-f --file', 'file to format'
  f\args 1

  stdin = \flag '--stdin', 'parse stdin'

  exclude = \option '-x --exclude', 'pattern for paths to exclude'
  exclude\args '*'
  exclude\overwrite false

  write = \flag '-w --write', 'write changes to disk'

  \mutex d, f, dof, stdin

args = argp\parse!

if args.stdin
  src = io.read 'a*'

  ret, err = formatter.format src
  if err
    io.stderr\write err
    os.exit 1

  io.stdout\write ret
  return

files = {}
if args.dir_or_file
  if fs.is_dir args.dir_or_file
    unless args.write
      io.stderr\write "dir passed to dir_or_file (#{ args.dir_or_file }), use -w"
      os.exit 1

    files = fs.find_files args.dir_or_file, 'yue', args.exclude
  else
    files[] = args.dir_or_file
elseif args.directory
  files = fs.find_files args.directory, 'yue', args.exclude
elseif args.file
  files[] = args.file
else
  io.stderr\write "unknown mode: #{ json.encode args }"

for file in *files
  src = fs.read_file file

  ret, err = formatter.format src
  if err
    io.stderr\write err
    os.exit 1

  unless args.write
    io.stdout\write ret
    continue

  fs.write_file file, ret
  io.stdout\write "formatted #{ file }\n"


